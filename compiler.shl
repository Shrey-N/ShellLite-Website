define page CompilerPage
    html
        head
            title "ShellLite Online Compiler"
            link rel="stylesheet" href="/static/style.css"
        body
            header
                div class="container"
                    nav
                        div class="logo" "ShellLite"
                        div class="nav-links"
                            a href="/" "Home"
                            a href="/compiler" class="active" "Compiler"
                            a href="/docs" "Docs"

            div class="container"
                div class="editor-container"
                    div class="editor-pane"
                        div class="pane-header"
                            h3 "Code Editor"
                            button class="run-btn" onclick="runCode()" "Run Code >"
                        textarea id="code-input" "say 'Btw this compiler was also written purely using ShellLite :)'"
                    
                    div class="output-pane"
                        div class="pane-header"
                            h3 "Output"
                        div id="output-display" class="output-content" "// Output will appear here..."

            script src="/static/compiler.js"
            
            footer
                div class="container"
                    p "Powered by ShellLite Web Runtime. We wrote everything using ShellLite only."

when someone visits "/compiler"
    CompilerPage

when someone submits to "/api/run"
    use python "random"
    use python "os"
    use python "sys"
    use python "subprocess"

    req_data = request.json
    code = req_data["code"]
    
    # Generate unique filename to avoid concurrency issues
    id = random.randint(1000, 999999)
    filename = "run_" + str(id) + ".shl"
    
    write code to file filename
    
    result = "No output returned by compiler."

    try:
        # Use sys.executable to ensure we use the same python that has shell_lite installed
        cmd = [sys.executable, "-m", "shell_lite.main", "run", filename]
        
        # Run with timeout to prevent infinite loops (e.g. 5 seconds)
        proc = subprocess.run(cmd, capture_output=True, text=True, timeout=5)
        
        if proc.returncode != 0:
             # If stderr has something, show it
             if proc.stderr:
                 result = "Error:\n" + proc.stderr
             else:
                 result = proc.stdout # sometimes errors are in stdout due to print logic
        else:
             result = proc.stdout
             if result == "":
                 result = "Executed successfully (No Output)"
             
    catch e:
        result = "Server Error: " + str(e)

    
    try:
        os.remove(filename)
    catch ign:
        # ignore error if file not found
        "" 
    
    return result
